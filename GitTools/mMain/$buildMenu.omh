# Remove all current menu entries
While $cinst.$objs.$count()>0
	Do $cinst.$objs.$remove($cinst.$objs.$first())
End While

Calculate lvLibraries as tvGitTools.$libraries
Do lvLibraries.$sort($ref.id,kFalse)
For lvLibraries.$line from 1 to lvLibraries.$linecount step 1
	Do $cinst.$objs.$add(lvLibraries.id,kTrue) Returns lvMenuEntry
	Calculate lvMenuEntry.$cascade as con("mLibrary/",lvLibraries.id)
	Calculate lvMenuEntry.$menuinst().$libraryID as lvLibraries.id
End For

If lvLibraries.$linecount>1
	Do $cinst.$objs.$add("")
	
	Do $cinst.$objs.$add("Import all") Returns lvMenuEntry
	Calculate lvMenuEntry.$enabled as kTrue
	Do lvMenuEntry.$methods.$add("$event") Returns lvEventMethod
	Calculate lvEventMethod.$methodtext as con("On evClick",kCr,"Do $cinst.$importAll()")
	
	Do $cinst.$objs.$add("Export all") Returns lvMenuEntry
	Calculate lvMenuEntry.$enabled as kTrue
	Do lvMenuEntry.$methods.$add("$event") Returns lvEventMethod
	Calculate lvEventMethod.$methodtext as con("On evClick",kCr,"Do $cinst.$exportAll()")
End If

If lvLibraries.$linecount>0
	Do $cinst.$objs.$add("")
End If

# Omnis Studio v10.x and below cannot automatically scan for libraries.
# For these versions, we'll provide a button to manually scan.
Calculate lvOmnisMajorVersion as pick(pos('.',sys(1))<>0,sys(1),left(sys(1),pos('.',sys(1))-1))
If lvOmnisMajorVersion<=10
	Do $cinst.$objs.$add("Scan for libraries") Returns lvMenuEntry
	Calculate lvMenuEntry.$enabled as kTrue
	Do lvMenuEntry.$methods.$add("$event") Returns lvEventMethod
	Calculate lvEventMethod.$methodtext as con("On evClick",kCr,"Do $cinst.$registerLibraries()")
End If

Do $cinst.$objs.$add("Register library") Returns lvMenuEntry
Calculate lvMenuEntry.$enabled as kTrue
Do lvMenuEntry.$methods.$add("$event") Returns lvEventMethod
Calculate lvEventMethod.$methodtext as con("On evClick",kCr,"Do $cinst.$registerLibrary()")

Do $cinst.$objs.$add("Settings") Returns lvMenuEntry
Calculate lvMenuEntry.$enabled as kTrue
Calculate lvMenuEntry.$cascade as "mSettings"